#include <Wire.h>

#define I2C_ADDR 0x42

static uint8_t sbusFrame[25];
static uint8_t idx = 0;

uint16_t ch[16];

volatile uint8_t page = 0;          // 0=CH1..8, 1=CH9..16
volatile uint8_t statusFlags = 0;   // bit0=failsafe, bit1=lostFrame
volatile uint16_t frameCount = 0;   // increments every decoded SBUS frame
volatile uint8_t sbusAlive = 0;

void decodeSbus(const uint8_t *b) {
  ch[0]  = ((b[1]    | b[2]  << 8)                          & 0x07FF);
  ch[1]  = ((b[2]>>3 | b[3]  << 5)                          & 0x07FF);
  ch[2]  = ((b[3]>>6 | b[4]  << 2 | b[5]  << 10)            & 0x07FF);
  ch[3]  = ((b[5]>>1 | b[6]  << 7)                          & 0x07FF);
  ch[4]  = ((b[6]>>4 | b[7]  << 4)                          & 0x07FF);
  ch[5]  = ((b[7]>>7 | b[8]  << 1 | b[9]  << 9)             & 0x07FF);
  ch[6]  = ((b[9]>>2 | b[10] << 6)                          & 0x07FF);
  ch[7]  = ((b[10]>>5| b[11] << 3)                          & 0x07FF);
  ch[8]  = ((b[12]   | b[13] << 8)                          & 0x07FF);
  ch[9]  = ((b[13]>>3| b[14] << 5)                          & 0x07FF);
  ch[10] = ((b[14]>>6| b[15] << 2 | b[16] << 10)            & 0x07FF);
  ch[11] = ((b[16]>>1| b[17] << 7)                          & 0x07FF);
  ch[12] = ((b[17]>>4| b[18] << 4)                          & 0x07FF);
  ch[13] = ((b[18]>>7| b[19] << 1 | b[20] << 9)             & 0x07FF);
  ch[14] = ((b[20]>>2| b[21] << 6)                          & 0x07FF);
  ch[15] = ((b[21]>>5| b[22] << 3)                          & 0x07FF);

  statusFlags = 0;
  if (b[23] & (1 << 3)) statusFlags |= 0x01; // failsafe (may vary by RX)
  if (b[23] & (1 << 2)) statusFlags |= 0x02; // lost frame (may vary by RX)

  sbusAlive = 1;
  frameCount++;
}

void onI2CReceive(int n) {
  if (n >= 1) page = (Wire.read() ? 1 : 0);
  while (Wire.available()) Wire.read();
}

void onI2CRequest() {
  // 1(alive) + 2(frameCount) + 16(8ch) + 1(flags) = 20 bytes
  Wire.write(sbusAlive);
  Wire.write((uint8_t)(frameCount & 0xFF));
  Wire.write((uint8_t)(frameCount >> 8));

  int start = page ? 8 : 0;
  for (int i = 0; i < 8; i++) {
    uint16_t v = ch[start + i];
    Wire.write((uint8_t)(v & 0xFF));
    Wire.write((uint8_t)(v >> 8));
  }

  Wire.write(statusFlags);
}

void setup() {
  Serial.begin(100000, SERIAL_8E2); // SBUS on D0
  Wire.begin(I2C_ADDR);
  Wire.onReceive(onI2CReceive);
  Wire.onRequest(onI2CRequest);
}

void loop() {
  while (Serial.available()) {
    uint8_t b = (uint8_t)Serial.read();

    if (idx == 0) {
      if (b != 0x0F) continue;
      sbusFrame[idx++] = b;
      continue;
    }

    sbusFrame[idx++] = b;
    if (idx == 25) {
      idx = 0;
      decodeSbus(sbusFrame);
    }
  }
}
